import io.franzbecker.gradle.lombok.task.DelombokTask
import xyz.wagyourtail.unimined.api.mappings.MappingNamespace

// without this block gradle seems to download maven metadata for log4j everytime gradle is configuring something
buildscript {
    dependencies {
        constraints {
            //noinspection ForeignDelegate
            classpath('org.apache.logging.log4j:log4j-core') {
                version {
                    strictly('[2.17, 3[')
                    prefer('2.17.0')
                }
                because('CVE-2021-44228, CVE-2021-45046, CVE-2021-45105')
            }
        }
    }
}

plugins {
    id 'io.franzbecker.gradle-lombok' version '5.0.0'
    id 'xyz.wagyourtail.unimined' version 'b536a54772' apply false
    id 'com.github.johnrengelman.shadow' version '7.0.0' apply false
    // id "com.dorongold.task-tree" version "2.1.1" to find the dependencies of runClient for the docker file
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'idea'
    apply plugin: 'maven-publish'
    apply plugin: 'io.franzbecker.gradle-lombok'

    ext {
        nameNoPb = project.name.split("-").length > 1 ? project.name.split("-")[1] : project.name
        cap_type = nameNoPb.substring(0, 1).toUpperCase() + nameNoPb.substring(1)
        sub_type = cap_type.toLowerCase()
    }

    repositories {
        maven {
            name = 'SpongeMaven'
            url = 'https://repo.spongepowered.org/repository/maven-public/'
        }

        maven {
            name = 'FabricMaven'
            url = 'https://maven.fabricmc.net/'
        }

        maven {
            name = 'JitpackMaven'
            url = 'https://jitpack.io'
        }

        maven {
            name = '3arthMaven'
            url = 'https://3arthqu4ke.github.io/maven'
        }

        maven {
            name = "ParchmentmcMaven"
            url = "https://maven.parchmentmc.net/"
        }

        mavenCentral()
    }

    archivesBaseName = project.name.toLowerCase()
    version = "${project.property('mc_version')}-${project.property('project_version')}"
    group = 'me.earth.pingbypass'
    targetCompatibility = sourceCompatibility = JavaVersion.VERSION_17

    configurations {
        jarLibsApi
        jarLibsImplementation
        lwjglAgent.extendsFrom runtimeOnly
    }

    dependencies {
        compileOnly 'org.jetbrains:annotations:24.0.1'

        compileOnly 'org.projectlombok:lombok:1.18.26'
        annotationProcessor 'org.projectlombok:lombok:1.18.26'

        testCompileOnly 'org.projectlombok:lombok:1.18.26'
        testAnnotationProcessor 'org.projectlombok:lombok:1.18.26'

        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.2'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.2'

        if (rootProject.property('hmc.lwjgl').toBoolean()) {
            lwjglAgent "me.earth.headlessmc:headlessmc-lwjgl:1.8.1"
        }

        api configurations.jarLibsApi
        implementation configurations.jarLibsImplementation
    }

    test {
        useJUnitPlatform()
        testLogging {
            showStandardStreams = true
            events "failed"
            exceptionFormat "full"
        }
    }

    def dependsOnBeforeJavadocAndSourcesJar = new HashSet<>(assemble.dependsOn)

    java {
        withSourcesJar()
        withJavadocJar()
    }

    assemble {
        // I do not want to build the javadocJar every time
        // TODO: better way? dependsOn contains TaskContainers, check each for task?
        dependsOn.removeIf { !dependsOnBeforeJavadocAndSourcesJar.contains(it) }
    }

    // TODO: even though we are doing all of this Intellij still complains about bytecode not matching the source...
    // (actually not if the files are local???)
    if (sourceSets.main.java.srcDirs.stream().anyMatch { it.exists()}) {
        tasks.register('delombok', DelombokTask) {
            dependsOn compileJava
            ext.outputDir = file(buildDir.toPath().resolve('delombok'))
            outputs.dir(ext.outputDir)
            sourceSets.main.java.srcDirs.each {
                if (it.exists()) {
                    inputs.dir(it)
                    // not using -f pretty for now, we might want to use some of these
                    args(it, '-f', 'suppressWarnings:skip',
                             '-f', 'generated:skip',
                             '-f', 'danceAroundIdeChecks:skip',
                             '-f', 'generateDelombokComment:skip',
                             '-f', 'javaLangAsFQN:skip',
                             '-d', ext.outputDir)
                }
            }
        }

        javadoc {
            dependsOn delombok
            source = delombok.outputDir
            options.addStringOption('Xdoclint:none', '-quiet')
            options.linkSource true
        }

        sourcesJar {
            dependsOn delombok
            from delombok.outputDir
            // I tried every single exclude/include pattern but I could not get any to work
            exclude (fileTreeElement -> {
                return !((FileTreeElement) fileTreeElement)
                        .getFile()
                        .toPath()
                        .toAbsolutePath()
                        .startsWith(((File) delombok.outputDir).toPath().toAbsolutePath())
            })
        }
    }

    tasks.withType(JavaCompile).configureEach {
        if (JavaVersion.current().isJava9Compatible()) {
            it.options.release.set(17)
        }
    }

    // This allows Intellij to find ByteCode for our classes after a jar has been built.
    // Helpful for writing the Mixins in pb-security.
    idea {
        module {
            inheritOutputDirs = false
            outputDir = compileJava.destinationDirectory.getAsFile().getOrNull()
            testOutputDir = compileTestJava.destinationDirectory.getAsFile().getOrNull()
        }
    }

    def forgeRunnerPrjs = [project(':pb-server-forgerunner'), project(':pb-client-forgerunner')]
    def forgePrjs = [project(':pb-client-forge'), project(':pb-server-forge')] + forgeRunnerPrjs
    def fabricPrjs = [project(':pb-client-fabric'), project(':pb-server-fabric')]
    def javaDocPrjs = [project(':pb-commons'), project(':pb-client-fabric'), project(':pb-server-fabric')]
    def commonsPrjs = [project(':pb-client'), project(':pb-server')]

    def runClientPrjs = forgeRunnerPrjs + fabricPrjs
    def platformPrjs = forgePrjs + fabricPrjs
    def modPrjs = platformPrjs + project(':pb-security-plugin')
    // I did not want to make the API depend on Minecraft,
    // but at the same time I wanted to use brigadier without any generics
    def mcProjects = platformPrjs + commonsPrjs + project(':pb-commons') + project(':pb-api')
    def mixinProjects = mcProjects + project(':pb-security-plugin')
    def gsonProjects = mcProjects + project(':pb-api')

    if (gsonProjects.contains(project)) {
        dependencies {
            // TODO: Theoretically it is correct to expose this as api since we expose interfaces using gson classes
            //  But this means that in plugins, to get forge to work in gradle we need to exclude it on every dependency
            api 'com.google.code.gson:gson:2.10.1'
        }
    }

    if (mixinProjects.contains(project)) {
        dependencies {
            compileOnly 'org.spongepowered:mixin:0.8.5'
        }
    }

    if (commonsPrjs.contains(project)) {
        dependencies {
            api project(':pb-api')
            api project(':pb-security-api')
            api project(':pb-security-lib')
            api project(':pb-commons')
        }
    }

    if (javaDocPrjs.contains(project)) {
        configurations {
            javaDocLibs
        }

        afterEvaluate {
            javadoc {
                classpath += files(configurations.javaDocLibs.resolve())
            }

            if (tasks.find { it.name == 'delombok' }) {
                delombok {
                    classpath += files(configurations.javaDocLibs.resolve())
                }
            }
        }
    }

    if ([project(':pb-server-fabric'), project(':pb-server-forge')].contains(project)) {
        dependencies {
            jarLibsApi project(':pb-server')
        }
    }

    if ([project(':pb-client-fabric'), project(':pb-client-forge')].contains(project)) {
        dependencies {
            jarLibsApi project(':pb-client')
        }
    }

    if (mcProjects.contains(project)) {
        apply plugin: 'xyz.wagyourtail.unimined'

        unimined {
            useGlobalCache = false
        }

        dependencies {
            minecraft "net.minecraft:minecraft:${rootProject.mc_version}"
            mappings "net.fabricmc:intermediary:${rootProject.mc_version}:v2"
            mappings "net.minecraft:minecraft:${rootProject.mc_version}:client-mappings"
            // parchment should resolve some parameter names
            mappings "org.parchmentmc.data:parchment-1.20.1:2023.07.30@zip"
        }

        // TODO: this can cause problems with other processes (projects) trying to access the minecraft libraries
        afterEvaluate {
            javadoc {
                classpath += files(configurations.minecraftLibraries.resolve())
                classpath += files(configurations.minecraft.resolve())
            }

            if (tasks.find { it.name == 'delombok' }) {
                delombok.classpath += files(configurations.minecraftLibraries.resolve())
                delombok.classpath += files(configurations.minecraft.resolve())
            }
        }

        remapJar {
            archiveClassifier.set('release')
        }
    }

    if (platformPrjs.contains(project)) {
        apply plugin: 'com.github.johnrengelman.shadow'

        dependencies {
            jarLibsApi project(':pb-api')
            jarLibsApi project(':pb-security-api')
            jarLibsApi project(':pb-security-lib')
            jarLibsApi project(':pb-commons')

            // Added because Runtime Remapping does not seem to remap methods rn if we just place the jar in the mods folder
            if (rootProject.property('hmc.specifics').toBoolean()) {
                // TODO: instead of 1.20.1-1.2.2 use set mc version
                // TODO: Hmc-Specifics: Bad Maven pub leads to lots of warnings about duplicate classes!
                modImplementation "me.earth.headlessmc:hmc-specifics-fabric:1.20.1-1.2.2"
            }
        }

        shadowJar {
            configurations = [project.configurations.jarLibsApi, project.configurations.jarLibsImplementation]
            classifier 'fat'
            exclude '**/module-info.class'
            exclude '**/META-INF/maven/'
            exclude '**/com/google/gson/**'
        }

        remapJar {
            inputFile.set(shadowJar.archiveFile)
            dependsOn(shadowJar)
            exclude '**/module-info.class'
            exclude '**/META-INF/maven/'
            exclude '**/com/google/gson/**'
        }
    }

    if (runClientPrjs.contains(project)) {
        afterEvaluate {
            runClient {
                standardInput = System.in
                // Without this fix unimined 0.3 adds " around the path, at least on unix, like this:
                // "/home/.../pingbypass/build/unimined/remapClasspath.txt"
                systemProperties['fabric.remapClasspathFile'] = rootProject.buildDir.toPath().resolve("unimined").resolve("remapClasspath.txt").toAbsolutePath()
                //jvmArgs += ['-Dmixin.debug.export=true']
                if (rootProject.property('hmc.lwjgl').toBoolean()) {
                    jvmArgs += ["-javaagent:${configurations.lwjglAgent.files.iterator().next()}"]
                    systemProperties['joml.nounsafe'] = 'true'
                    systemProperties['fabric.systemLibraries'] = "${configurations.lwjglAgent.files.iterator().next()}"
                }
            }
        }

        tasks.register('runClientWithoutDependencies') {
            doLast {
                project['runClient'].exec()
            }
        }
    }

    if (forgePrjs.contains(project)) {
        ext {
            type = 'forge'
        }

        minecraft {
            forge {
                devFallbackNamespace = MappingNamespace.INTERMEDIARY
                mixinConfig = [
                    "mixins.pingbypass_${project.ext.sub_type}.json".toString(),
                    "mixins.pingbypass_commons.json"
                ]
            }
        }

        dependencies {
            forge "net.minecraftforge:forge:${rootProject.mc_version}-${rootProject.forge_version}"
        }

        remapJar {
            targetNamespace = MappingNamespace.SEARGE
        }
    }

    if (fabricPrjs.contains(project)) {
        ext {
            type = 'fabric'
        }

        minecraft {
            fabric()
        }

        dependencies {
            fabric "net.fabricmc:fabric-loader:${rootProject.fabric_version}"
            javaDocLibs "net.fabricmc:fabric-loader:${rootProject.fabric_version}"
        }

        remapJar {
            targetNamespace = MappingNamespace.INTERMEDIARY
        }
    }

    if (modPrjs.contains(project)) {
        jar {
            if (project != project(':pb-security-plugin')) {
                // archiveClassifier.set('dev')
            }

            exclude '**/module-info.class'
            exclude '**/META-INF/maven/'
            exclude '**/com/google/gson/**'
            manifest {
                attributes(
                    'MixinConfigs': (project == project(':pb-security-plugin'))
                        ? 'mixins.pingbypass_security.json'
                        : "mixins.pingbypass_${project.ext.sub_type}.json,mixins.pingbypass_commons.json",
                    'MixinConnector': "me.earth.pingbypass.${project.ext.sub_type}.launch.${project.ext.cap_type}MixinConnector",
                    'Implementation-Title': "PingBypass-${project.ext.cap_type}",
                    'Implementation-Version': project.version
                )
            }
        }
    }

    jar {
        // i do not like the -dev classifier
        archiveClassifier.set('')
    }
}

dependencies {
    // makes the pom of the 'pingbypass' maven artifact contain all modules
    api project(':pb-api')
    api project(':pb-security-api')
    api project(':pb-security-lib')
    api project(':pb-client')
    api project(':pb-server')
    api project(':pb-commons')
}

def pubSuffix = System.getenv('IS_MAVEN_PUB') != null
    ? ''
    : System.getenv('GITHUB_RUN_NUMBER') != null && System.getenv('GITHUB_SHA') != null
        ? "-${System.getenv('GITHUB_RUN_NUMBER')}-${System.getenv('GITHUB_SHA').substring(0, 7)}"
        : '-local'

allprojects {
    if (!project.name.endsWith('forgerunner')) {
        publishing {
            publications {
                "$project.name"(MavenPublication) {
                    ((MavenPublication) it).groupId "${project.group}"
                    ((MavenPublication) it).artifactId "${project.archivesBaseName.toLowerCase()}"
                    ((MavenPublication) it).version "${project.version}${pubSuffix}"
                    //noinspection GroovyAssignabilityCheck
                    from components.java
                }
            }

            repositories {
                // mavenLocal()
                // maven {
                //     name = 'SomewhereOnThisMachine'
                //     url = 'file://...'
                // }

                if (System.getenv("PB_OTHER_REPO") != null) {
                    maven {
                        name = 'otherrepo'
                        url = "${System.getenv("PB_OTHER_REPO")}"
                    }
                }

                if (System.getenv('DEPLOY_TO_GITHUB_PACKAGES_URL') == null) {
                    maven {
                        name = 'BuildDirMaven'
                        url = rootProject.layout.buildDirectory.dir('maven')
                    }
                } else {
                    maven {
                        name = 'GithubPagesMaven'
                        url = System.getenv('DEPLOY_TO_GITHUB_PACKAGES_URL')
                        credentials {
                            username = System.getenv('GITHUB_USER')
                            password = System.getenv('GITHUB_TOKEN')
                        }
                    }
                }
            }
        }

        publish {
            dependsOn(javadocJar)
            dependsOn(sourcesJar)
            dependsOn(build)
        }
    }
}

publish {
    dependsOn(subprojects.build)
    dependsOn(subprojects.publish)
}

subprojects {
    afterEvaluate {
        tasks.withType(GenerateMavenPom).configureEach {
            it.pom.withXml {
                asNode().dependencies.dependency.each { dependency ->
                    // Since mixin is runtime scope and needs an additional repository it might be better to exclude it
                    if (dependency.artifactId.last().value().last() in ['fabric-loader', 'unspecified', 'mixin']) {
                        assert dependency.parent().remove(dependency)
                    }
                }
            }
        }
    }
}

// TODO: configure Intellij run 'Minecraft Client' to run pb client + server?
// TODO: do we really want to run the game in the deep subdirectories? Make run directory configurable?
tasks.register('runPBClientFabric') {
    description = 'Runs the PingBypass-Client on Fabric'
    group = 'run'
    dependsOn(project(':pb-client-fabric')['runClient'])
}

tasks.register('runPBClientForge') {
    description = 'Runs the PingBypass-Client on Forge'
    group = 'run'
    dependsOn(project(':pb-client-forgerunner')['runClient'])
}

tasks.register('runPBServerFabric') {
    description = 'Runs the PingBypass-Server on Fabric'
    group = 'run'
    dependsOn(project(':pb-server-fabric')['runClient'])
}

tasks.register('runPBServerForge') {
    description = 'Runs the PingBypass-Server on Forge'
    group = 'run'
    dependsOn(project(':pb-server-forgerunner')['runClient'])
}

tasks.register('cleanExceptUniminedCache') {
    description = 'Cleans everything except the unimined cache'
    group = 'build'
    delete layout.buildDirectory.dir('libs')
    delete layout.buildDirectory.dir('tmp')
    dependsOn(subprojects.clean)
}
